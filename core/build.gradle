buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.9"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'
apply plugin: 'net.ltgt.apt'

archivesBaseName = 'quickdto'
group = 'org.reladev.quickdto'
version = '1.0.0_RC1-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile files("${System.properties['java.home']}/../lib/tools.jar")

    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile group: 'com.matthew-tamlin', name: 'avatar', version: '2.1.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.21.0'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.11.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
   from sourceSets.main.allJava
}

javadoc {
    options.addStringOption("sourcepath", "src/")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment)}

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name 'QuickDto'
               artifactId 'quickdto'
               packaging 'jar'
               description 'DTO generator for handling coping dirty data'
               url 'https://github.com/reladev/quickdto'

               scm {
                   url 'scm:git:https://github.com/reladev/quickdto.git'
                   connection 'scm:git:https://github.com/reladev/quickdto.git'
                   developerConnection 'scm:git:https://github.com/reladev/quickdto.git'
               }

               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'jeffgnpc'
                       name 'Jeffrey Kleiss'
                   }
               }
           }
        }
    }
}

